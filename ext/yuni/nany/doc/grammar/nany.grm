"Name"     = 'Nany'
"Author"   = 'Bruno BORRI, Damien GERARD'
"Version"  = '0.1'
"About"    = 'Grammar for the Nany language, from the Yuni project team'
"Character Mapping" = 'Unicode'
"Case Sensitive" = 'True'
"Start Symbol" = <Program>

! -------------------------------------------------
! Character Sets
! -------------------------------------------------

{ID Head}      = {Letter} + {Letter Extended}
{ID Tail}      = {Alphanumeric} + [_]
{String Chars} = {Printable} + {HT} - ["\]
{Char Chars}   = {Printable} - ['']
{Hex Digit}    = {Digit} + [abcdef] + [ABCDEF]

! -------------------------------------------------
! Terminals
! -------------------------------------------------

Identifier    = {ID Head}{ID Tail}*
BooleanLiteral= 'true' | 'false'
StringLiteral = '"' ( {String Chars} | '\' {Printable} )* '"'
DecLiteral     = {Digit}+            ( [UuLl] | [Uu][Ll] | [Ll][Uu] )?
HexLiteral     = '0'[xX]{Hex Digit}+ ( [UuLl] | [Uu][Ll] | [Ll][Uu] )?
RealLiteral    = {Digit}*'.'{Digit}+ ( [FfDd] )?
CharLiteral    = '' ( {Char Chars} | '\'{Printable} ) ''
TypeQualifier  = 'const' | 'ref'
VisibilityQualifier = 'public' | 'protected' | 'private' | 'published'
OptimQualifier = 'immutable' | 'stable' | 'threadunsafe' | 'volatile'
BuiltInType    = 'void' | 'any' | 'int' | 'uint' | 'int8' | 'uint8' | 'int16' | 'uint16' | 'int32' | 'uint32' | 'int64' | 'uint64' | 'char' | 'float' | 'double' | 'bool' | 'string'

! -------------------------------------------------
! Comments
! -------------------------------------------------

Comment Line = '#' | '//'
Comment Start = '#*' | '/*'
Comment End = '*#' | '*/'

! -------------------------------------------------
! Program / Unit
! -------------------------------------------------

! The grammar starts below
<Program> ::= <Unit Declaration> <Dependencies> <Declaration List>

<Unit Declaration> ::= <Optional Visibility Qualifier> 'unit' Identifier ';'
               | ! Empty

<Dependencies> ::= <Dependency> <Dependencies>
               | ! Empty

<Dependency>   ::= 'uses' Identifier <Dependency Continued> ';'

<Dependency Continued> ::= '.' Identifier <Dependency Continued>
               | ! Empty

<Declaration List> ::= <Function Declaration> <Declaration List>
               | <Class Declaration> <Declaration List>
               | <Workflow Declaration> <Declaration List>
               | <Enum Declaration> <Declaration List>
               | <Typedef> ';' <Declaration List>
               | ! Empty

! -------------------------------------------------
! Literals
! -------------------------------------------------

<Literal> ::= BooleanLiteral
               |  DecLiteral
               |  HexLiteral
               |  RealLiteral
               |  CharLiteral
               |  StringLiteral
               |  BuiltInType
               |  'nil'
               |  'self'

! -------------------------------------------------
! Classes
! -------------------------------------------------

<Class Declaration> ::= 'class' Identifier <Optional Type Parameters> <Optional Base Classes> '{' <Class Content> '}'

<Anonymous Class Declaration> ::= 'class' <Optional Type Parameters> <Optional Base Classes> '{' <Class Content> '}'

<Optional Base Classes> ::= ':' <SingleThread Exp> <Optional Base Classes Continued>
               | ! Empty

<Optional Base Classes Continued> ::= ',' <SingleThread Exp>
               | ! Empty

! TODO
<Class Content> ::= VisibilityQualifier <Class Content>
               | <Method Declaration> <Class Content>
               | <View Declaration> <Class Content>
               | <Attribute Declaration> ';' <Class Content>
               | <Property Declaration> ';' <Class Content>
               | <Class Declaration> <Class Content> ! Nested class
               | <Typedef> ';' <Class Content>
               | ! Empty

<Property Declaration> ::= <Optional Abstract> 'property' Identifier <Typing> <Assignment> <Property Callbacks>
               | <Optional Abstract> 'property' Identifier <Assignment> <Property Callbacks>
               | <Optional Abstract> 'property' Identifier <Typing> <Property Callbacks>

<Optional Abstract> ::= 'abstract'
               | ! Empty

<Property Callbacks> ::= 'read' <SingleThread Exp> <Property Callbacks>
               | 'write' <SingleThread Exp> <Property Callbacks>
               | 'read' <Property Callbacks>
               | 'write' <Property Callbacks>
               | ! Empty

<Attribute Declaration> ::= 'var' Identifier <Typing> <Assignment>
               | 'var' Identifier <Assignment>
               | 'var' Identifier <Typing>

<Assignment> ::= ':=' <SingleThread Exp>

<Typing> ::= ':' <Simple Exp>
               | ':' <Type Qualifiers> <Typing Continued>

<Typing Continued> ::= <Simple Exp>
               | ! Empty

! -------------------------------------------------
! Workflows
! -------------------------------------------------

<Workflow Declaration> ::= 'workflow' Identifier '{' <Workflow Content> '}'

<Workflow Content> ::= <State Block> <Transition Block>

<State Block> ::= 'states' <Workflow States>
               | ! Empty

<Workflow States> ::= 'default' Identifier ';' <Workflow States>
               | 'state' Identifier ';' <Workflow States>
               | ! Empty

<Transition Block> ::= 'transitions' <Workflow Transitions>
               | ! Empty

<Workflow Transitions> ::= 'default' 'allow' ';' <Workflow Transitions>
               | 'default' 'forbid' ';' <Workflow Transitions>
               | 'allow' <Workflow Permissions> '=>' <Workflow Permissions> ';' <Workflow Transitions>
               | 'forbid' <Workflow Permissions> '=>' <Workflow Permissions> ';' <Workflow Transitions>
               | ! Empty

<Workflow Permission> ::= '*'
               | Identifier
               | '+' Identifier
               | '-' Identifier

<Workflow Permissions> ::= <Workflow Permission> ',' <Workflow Permissions>
               | <Workflow Permission>

! -------------------------------------------------
! Enums
! -------------------------------------------------

<Enum Declaration> ::= 'enum' Identifier '{' <Enum Content> '}'

<Enum Content> ::= Identifier ',' <Enum Content>
               | Identifier
               | ! Empty


! -------------------------------------------------
! Functions and methods
! -------------------------------------------------

<Function Declaration> ::= <Optional Optim Qualifier> 'function' Identifier <Optional Type Parameters> <Optional Parameters> <Return Type Declaration> <Function Body>

<Anonymous Function Declaration> ::= <Optional Optim Qualifier> 'function' <Optional Type Parameters> <Optional Parameters> <Return Type Declaration> <Function Body>

<Method Declaration> ::= <Optional Optim Qualifier> 'method' Identifier <Optional Type Parameters> <Optional Parameters> <Return Type Declaration> <Function Body>
               | <Optional Optim Qualifier> 'method' Identifier <Optional Type Parameters> <Optional Parameters> <Return Type Declaration> ';' ! Abstract method declaration

<View Declaration> ::= <Optional Optim Qualifier> 'view' Identifier <Optional Type Parameters> <Optional Parameters> <Return Type Declaration> <Function Body>
               | <Optional Optim Qualifier> 'view' Identifier <Optional Type Parameters> <Optional Parameters> <Return Type Declaration> ';' ! Abstract view declaration

<Function Body> ::= '{' <Expression> '}'
               | '{' '}' ! Empty body

<Return Type Declaration> ::= ':' <Optional Type Qualifiers> <SingleThread Exp>
               | ! Empty

<Optional Parameters> ::= '(' <Parameter List> ')'
               | ! Empty

<Parameter List> ::= Identifier <Typing> <Assignment> <Parameter List Continued>
               | Identifier <Assignment> <Parameter List Continued>
               | Identifier <Typing> <Parameter List Continued>
               | Identifier <Parameter List Continued>
               | ! Empty

<Parameter List Continued> ::= ',' <Parameter List>
               | ! Empty

<Optional Type Qualifiers> ::= <Type Qualifiers>
               | ! Empty

<Type Qualifiers> ::= TypeQualifier <Type Qualifiers Continued>

<Type Qualifiers Continued> ::= TypeQualifier <Type Qualifiers Continued>
               | ! Empty

<Optional Optim Qualifier> ::= OptimQualifier
               | ! Empty

<Optional Visibility Qualifier> ::= VisibilityQualifier
               | ! Empty

<Argument List> ::= <Possibly Parallel Exp> <Argument List Continued>
               | ! Empty

<Argument List Continued> ::= ',' <Possibly Parallel Exp> <Argument List Continued>
               | ! Empty

<Typedef> ::= 'typedef' Identifier ':=' <Possibly Parallel Exp>


! -------------------------------------------------
! Types
! -------------------------------------------------

<Optional Type Parameters> ::= <Type Parameters>
               | ! Empty

! This is for generics declarations
<Type Parameters> ::= '<:' Identifier <Type Parameters Continued> ':>'
               | '<:' Identifier ':=' <SingleThread Exp> <Type Parameters Continued> ':>'

<Type Parameters Continued> ::= ',' Identifier <Type Parameters Continued>
               | ',' Identifier ':=' <SingleThread Exp> <Type Parameters Continued>
               | ! Empty

<Type Arguments> ::= <SingleThread Exp> <Type Arguments Continued>
               | ! Empty

<Type Arguments Continued> ::= ',' <SingleThread Exp> <Type Parameters Continued>
               | ! Empty

! -------------------------------------------------
! Expressions
! -------------------------------------------------

<Expression>   ::= <Possibly Parallel Exp> <Expression List>

<Expression List> ::= ';' <Expression>
               | ';'

<Possibly Parallel Exp> ::= <SingleThread Exp>
               | '&' <SingleThread Exp>

<SingleThread Exp> ::= <Assignment Exp>

<Assignment Exp> ::= <Assignment Exp> ':=' <Is Exp>
               | <Local Declaration Exp>

<Local Declaration Exp> ::= 'var' <Value> <Typing>
               | <Is Exp>

<Is Exp>    ::= <Is Exp> 'is' <Anonymous Decl Exp>
               | <Anonymous Decl Exp>

<Anonymous Decl Exp> ::= <Simple Exp>
               | <Anonymous Function Declaration>
               | <Anonymous Class Declaration>

<Simple Exp> ::= <Binary Exp>
               | 'new' <Simple Exp>
               | <Typedef>
               | 'return' <SingleThread Exp>
               | 'break'
               | 'continue'
               | 'each' '(' <Expression> ')'
               | 'if' <Possibly Parallel Exp> 'then' <Possibly Parallel Exp> <Else Expression>
               | 'while' <Possibly Parallel Exp> 'do' <Possibly Parallel Exp> <Else Expression>
               | 'for' <Expression> 'do' <Possibly Parallel Exp> <Else Expression>
               | 'for' <Expression> 'order' <Order Type> 'do' <Possibly Parallel Exp> <Else Expression> 
               | 'for' Identifier 'in' <Expression> 'do' <Possibly Parallel Exp> <Else Expression>
               | 'for' Identifier 'in' <Expression> 'order' <Order Type> 'do' <Possibly Parallel Exp> <Else Expression> 
               | '{' <Expression> '}'
               | '{' '}'

<Order type> ::= 'asc'
               | 'asc' Identifier
               | 'desc'
               | 'desc' Identifier
               | ! Empty

<Else Expression> ::= 'else' <Possibly Parallel Exp>
               | ! Empty

<Binary Exp> ::= <Binary Exp> '|' <Xor Exp> ! Constraint
               |  <Xor Exp>

<Xor Exp>      ::= <Xor Exp> 'xor' <Or Exp> ! Boolean XOR
               |  <Or Exp>

<Or Exp>      ::= <Or Exp> 'or' <And Exp> ! Boolean Or
               |  <And Exp>

<And Exp>     ::= <And Exp> 'and' <Equal Exp> ! Boolean And
               |  <Equal Exp>

<Equal Exp>   ::= <Equal Exp> '='  <Compare Exp>    ! Equal
               |  <Equal Exp> '!=' <Compare Exp>    ! Not equal
               |  <Compare Exp>

<Compare Exp> ::= <Compare Exp> '<' <Regexp Exp>
               |  <Compare Exp> '>'  <Regexp Exp> 
               |  <Compare Exp> '<=' <Regexp Exp> 
               |  <Compare Exp> '>=' <Regexp Exp>
               |  <Regexp Exp>

! Is the priority correct ?
<Regexp Exp>  ::= <Regexp Exp> '~' <Shift Exp>
               |  <Shift Exp>

<Shift Exp>   ::= <Shift Exp> '<<' <Add Exp>
               |  <Shift Exp> '>>' <Add Exp>
               |  <Add Exp>

<Add Exp>     ::= <Add Exp> '+' <Mult Exp>
               |  <Add Exp> '-' <Mult Exp>
               |  <Mult Exp> 

<Mult Exp>    ::= <Mult Exp> '*' <Power Exp>
               |  <Mult Exp> '/' <Power Exp>
               |  <Mult Exp> '%' <Power Exp>
               |  <Power Exp>

<Power Exp>   ::= <Power Exp> '^' <As Exp>
               | <As Exp>

<As Exp>    ::= <As Exp> 'as' <Typeof Exp>
               | <Typeof Exp>

<Typeof Exp> ::= 'typeof' <Negate Exp>
               | <Negate Exp>

<Negate Exp>  ::= '-' <Value> 
               |  '--' <Value>
               |  '++' <Value>
               |  <Value> '--'
               |  <Value> '++'
               |  <Value>


! For use in arithmetic, requires parentheses around expressions to have proper priorities
! e.g. : weight := - (car.weight + passengers.weight)
<Value>  ::= <Literal> ! 2.4.round
               | <Value> '.' Identifier ! Property / attribute / method call with no argument
               | <Value> '(' <Argument List> ')' ! Function or method call with arguments
               | '(' <SingleThread Exp> ')'
               | Identifier
               | <Value> '[' ']'
               | <Value> '[' <SingleThread Exp> ']'
               | <Value> '<:' <Type Arguments> ':>'
               | '[' <Array Arguments> ']'
               | '[:' <Array Arguments> ':]'
               | '[:' <Array Arguments> ':['
               | ']:' <Array Arguments> ':]'
               | ']:' <Array Arguments> ':['

<Array Arguments> ::= <SingleThread Exp> <Array Arguments Continued>
               | <SingleThread Exp> '..' <SingleThread Exp> <Array Arguments Continued>

<Array Arguments Continued> ::= ! Empty
               | <SingleThread Exp> ',' <Array Arguments Continued>
               | <SingleThread Exp> '..' <SingleThread Exp> ',' <Array Arguments Continued>
